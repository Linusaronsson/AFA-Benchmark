# configfile: "workflow/conf/default.yaml"

import os

# Define global variables from config values
EXPERIMENT_ID = config["experiment_id"]
DATASET_ARTIFACT_ALIASES = config["dataset_artifact_aliases"]
DATASET_SPLITS = config["dataset_splits"]
DATASET_SPLIT_MODE = config["dataset_split_mode"]
PRETRAINED_MODEL_ALIASES = config["pretrained_model_aliases"]
CLASSIFIER = config["classifier"]
CLASSIFIER_ARTIFACT_ALIASES = config["classifier_artifact_aliases"]
VALIDATE_ARTIFACTS = config["validate_artifacts"]
SEEDS = config["seeds"]
TRAIN_DEVICE = config["train_device"]
EVAL_DEVICE = config["eval_device"]
COST_PARAMS_PER_METHOD = config["cost_params_per_method"]
SMOKE_TEST = config["smoke_test"]

METHODS_WITH_PRETRAIN_STAGE = set(PRETRAINED_MODEL_ALIASES.keys())
METHODS = list(COST_PARAMS_PER_METHOD.keys())
DATASETS = list(DATASET_ARTIFACT_ALIASES.keys())
COST_PARAMS_INDICES_PER_METHOD = {
    method: list(range(len(params)))
    for method, params in COST_PARAMS_PER_METHOD.items()
}


# Fast way to produce soft budget plot
rule soft_budget:
    input:
        f"experiments/{EXPERIMENT_ID}/results/soft_budget/plot.pdf",


rule soft_budget_train_all:
    input:
        [
            f"experiments/{EXPERIMENT_ID}/indicators/soft_budget/trained_method/{dataset}/split{dataset_split}/{method}/costparam{cost_param}/seed{seed}.done"
            for method in METHODS
            for dataset in DATASETS
            for dataset_split in DATASET_SPLITS
            for cost_param in COST_PARAMS_PER_METHOD[method]
            for seed in SEEDS
        ],


rule soft_budget_eval_all:
    input:
        [
            f"experiments/{EXPERIMENT_ID}/indicators/soft_budget/eval/{method}/{dataset}/split{dataset_split}/costparam{cost_param}/seed{seed}.done"
            for method in METHODS
            for dataset in DATASETS
            for dataset_split in DATASET_SPLITS
            for cost_param in COST_PARAMS_PER_METHOD[method]
            for seed in SEEDS
        ],


rule train_soft_budget_method:
    output:
        touch(
            f"experiments/{EXPERIMENT_ID}/indicators/soft_budget/trained_method/{{dataset}}/split{{dataset_split}}/{{method}}/costparam{{cost_param}}/seed{{seed}}.done"
        ),
    params:
        dataset_or_pretrain_artifact_name_arg=lambda wildcards: (
            f"pretrained_model_artifact_name=pretrain_{wildcards.method}-{wildcards.dataset}_split_{wildcards.dataset_split}:{PRETRAINED_MODEL_ALIASES[wildcards.method]}"
            if wildcards.method in METHODS_WITH_PRETRAIN_STAGE
            else f"dataset_artifact_name={wildcards.dataset}_split_{wildcards.dataset_split}:{DATASET_ARTIFACT_ALIASES[wildcards.dataset]}"
        ),
        dataset_specific_args=lambda wildcards: (
            f"dataset@_global_={wildcards.dataset}_smoke"
            if SMOKE_TEST
            else f"dataset@_global_={wildcards.dataset}"
        ),
    shell:
        """
            uv run scripts/train_methods/train_{wildcards.method}.py \
                {params.dataset_specific_args} \
                {params.dataset_or_pretrain_artifact_name_arg} \
                seed={wildcards.seed} \
                hard_budget=null \
                output_artifact_aliases=["{EXPERIMENT_ID}"] \
                cost_param={wildcards.cost_param} \
                device={TRAIN_DEVICE}
        """


rule eval_soft_budget_method:
    input:
        f"experiments/{EXPERIMENT_ID}/indicators/soft_budget/trained_method/{{dataset}}/split{{dataset_split}}/{{method}}/costparam{{cost_param}}/seed{{seed}}.done",
    output:
        touch(
            f"experiments/{EXPERIMENT_ID}/indicators/soft_budget/eval/{{method}}/{{dataset}}/split{{dataset_split}}/costparam{{cost_param}}/seed{{seed}}.done"
        ),
    params:
        classifier_arg=lambda wildcards: f"trained_classifier_artifact_name={CLASSIFIER}-{wildcards.dataset}_split_{wildcards.dataset_split}:{CLASSIFIER_ARTIFACT_ALIASES[wildcards.dataset]}",
    shell:
        """
        uv run scripts/evaluation/eval_soft_afa_method.py \
            trained_method_artifact_name=train_{wildcards.method}-{wildcards.dataset}_split_{wildcards.dataset_split}-costparam_{wildcards.cost_param}-seed_{wildcards.seed}:{EXPERIMENT_ID} \
            cost_param={wildcards.cost_param} \
            {params.classifier_arg} \
            output_artifact_aliases=["{EXPERIMENT_ID}"] \
            seed=null \
            device={EVAL_DEVICE} \
            eval_only_n_samples=null \
            dataset_split={DATASET_SPLIT_MODE} \
            validate_artifacts={VALIDATE_ARTIFACTS}
        """


rule download_soft_budget_eval_result:
    input:
        f"experiments/{EXPERIMENT_ID}/indicators/soft_budget/eval/{{method}}/{{dataset}}/split{{dataset_split}}/costparam{{cost_param}}/seed{{seed}}.done",
    output:
        f"experiments/{EXPERIMENT_ID}/results/soft_budget/eval/{{method}}/{{dataset}}/split{{dataset_split}}/costparam{{cost_param}}/seed{{seed}}/soft_eval_data.csv",
    shell:
        """
            uv run wandb artifact get train_{wildcards.method}-{wildcards.dataset}_split_{wildcards.dataset_split}-costparam_{wildcards.cost_param}-seed_{wildcards.seed}-{CLASSIFIER}-{wildcards.dataset}_split_{wildcards.dataset_split}:{EXPERIMENT_ID} --root results/soft_budget/eval/{wildcards.method}/{wildcards.dataset}/split{wildcards.dataset_split}/costparam{wildcards.cost_param}/seed{wildcards.seed}
        """


rule add_dataset_split_col_to_soft_budget_eval_result:
    input:
        f"experiments/{EXPERIMENT_ID}/results/soft_budget/eval/{{method}}/{{dataset}}/split{{dataset_split}}/costparam{{cost_param}}/seed{{seed}}/soft_eval_data.csv",
    output:
        f"experiments/{EXPERIMENT_ID}/results/soft_budget/eval/{{method}}/{{dataset}}/split{{dataset_split}}/costparam{{cost_param}}/seed{{seed}}/soft_eval_data_with_split.csv",
    conda:
        "envs/R.yaml"
    shell:
        """
        Rscript scripts/misc/add_dataset_split_col.R {input} {output} {wildcards.dataset_split}
        """


rule combine_soft_budget_eval_results:
    input:
        [
            f"experiments/{EXPERIMENT_ID}/results/soft_budget/eval/{method}/{dataset}/split{dataset_split}/costparam{cost_param}/seed{seed}/soft_eval_data_with_split.csv"
            for method in METHODS
            for dataset in DATASETS
            for dataset_split in DATASET_SPLITS
            for cost_param in COST_PARAMS_PER_METHOD[method]
            for seed in SEEDS
        ],
    output:
        f"experiments/{EXPERIMENT_ID}/results/soft_budget/eval/combined.csv",
    conda:
        "envs/R.yaml"
    shell:
        """
        Rscript scripts/misc/combine_soft_budget_results.R {input} {output}
        """


rule soft_budget_plot:
    input:
        f"experiments/{EXPERIMENT_ID}/results/soft_budget/eval/combined.csv",
    output:
        f"experiments/{EXPERIMENT_ID}/results/soft_budget/plot.pdf",
    conda:
        "envs/R.yaml"
    shell:
        """
        Rscript scripts/plotting/produce_soft_budget_plots.R {input[0]} {output}
        """
